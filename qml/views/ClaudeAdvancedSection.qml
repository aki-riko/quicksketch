import QtQuick
import QtQuick.Layouts
import PrismQML as Fluent

Fluent.Expander {
    id: root

    property string modelsValue: ""
    property string headersValue: ""
    property int headerCount: 0
    property bool clearHeadersValue: false

    signal modelsEdited(string value)
    signal headersEdited(string value)
    signal clearHeadersToggled(bool value)

    title: "模型与请求头"
    content: "高级配置 · 可选覆盖模型发现和额外请求头"
    expanded: false

    GridLayout {
        id: advancedGrid
        width: parent ? parent.width : 0
        columns: width < 680 ? 1 : 2
        uniformCellWidths: columns === 2
        columnSpacing: Fluent.Enums.spacing.l
        rowSpacing: Fluent.Enums.spacing.l
        readonly property real equalItemWidth: columns === 2
                                               ? Math.max(
                                                     0,
                                                     (width - columnSpacing) / 2
                                                 )
                                               : width

        ColumnLayout {
            Layout.fillWidth: true
            Layout.minimumWidth: 0
            Layout.preferredWidth: advancedGrid.equalItemWidth
            Layout.maximumWidth: advancedGrid.equalItemWidth
            Layout.alignment: Qt.AlignTop
            spacing: Fluent.Enums.spacing.xxs

            Text {
                Layout.fillWidth: true
                text: "模型列表"
                color: Fluent.Enums.textColor.secondary
                font.pixelSize: Fluent.Enums.typography.caption
                font.bold: true
                font.family: Fluent.Enums.fontFamily
            }
            RowLayout {
                Layout.fillWidth: true
                Layout.preferredHeight: Fluent.Enums.controlSize.checkboxOuter

                Text {
                    Layout.fillWidth: true
                    Layout.alignment: Qt.AlignVCenter
                    text: "每行一个，第一个为默认；留空使用 /v1/models"
                    color: Fluent.Enums.textColor.tertiary
                    font.pixelSize: Fluent.Enums.typography.caption
                    font.family: Fluent.Enums.fontFamily
                    elide: Text.ElideRight
                }
            }
            Fluent.TextEdit {
                id: modelsEdit
                objectName: "claudeModelsEdit"
                Layout.fillWidth: true
                Layout.preferredHeight: 108
                placeholderText: "claude-sonnet-4-6\nclaude-opus-4-6"
                Component.onCompleted: Qt.callLater(function() {
                    text = root.modelsValue
                })
                onTextEdited: {
                    if (text !== root.modelsValue) root.modelsEdited(text)
                }
                Connections {
                    target: root
                    function onModelsValueChanged() {
                        if (modelsEdit.text !== root.modelsValue) {
                            modelsEdit.text = root.modelsValue
                        }
                    }
                }
            }
        }

        ColumnLayout {
            Layout.fillWidth: true
            Layout.minimumWidth: 0
            Layout.preferredWidth: advancedGrid.equalItemWidth
            Layout.maximumWidth: advancedGrid.equalItemWidth
            Layout.alignment: Qt.AlignTop
            spacing: Fluent.Enums.spacing.xxs

            Text {
                Layout.fillWidth: true
                text: root.headerCount > 0
                      ? "额外请求头 · 已保存 " + root.headerCount + " 项"
                      : "额外请求头 · 可选"
                color: Fluent.Enums.textColor.secondary
                font.pixelSize: Fluent.Enums.typography.caption
                font.bold: true
                font.family: Fluent.Enums.fontFamily
            }
            RowLayout {
                Layout.fillWidth: true
                Layout.preferredHeight: Fluent.Enums.controlSize.checkboxOuter
                spacing: Fluent.Enums.spacing.s

                Text {
                    Layout.fillWidth: true
                    Layout.alignment: Qt.AlignVCenter
                    text: "输入 JSON 对象覆盖现有值；敏感内容不会回显"
                    color: Fluent.Enums.textColor.tertiary
                    font.pixelSize: Fluent.Enums.typography.caption
                    font.family: Fluent.Enums.fontFamily
                    elide: Text.ElideRight
                }

                Fluent.Toggle {
                    id: clearHeadersToggle
                    objectName: "claudeClearHeadersToggle"
                    Layout.alignment: Qt.AlignVCenter
                    enabled: root.headerCount > 0
                    visible: root.headerCount > 0
                    controlType: Fluent.Enums.toggle.control_checkbox
                    type: Fluent.Enums.toggle.type_default
                    text: "应用时删除"
                    Component.onCompleted: Qt.callLater(function() {
                        checked = root.clearHeadersValue
                    })
                    onToggled: function(checkedValue) {
                        root.clearHeadersToggled(checkedValue)
                    }
                    Connections {
                        target: root
                        function onClearHeadersValueChanged() {
                            if (clearHeadersToggle.checked !== root.clearHeadersValue) {
                                clearHeadersToggle.checked = root.clearHeadersValue
                            }
                        }
                    }
                }
            }
            Fluent.TextEdit {
                id: headersEdit
                objectName: "claudeHeadersEdit"
                Layout.fillWidth: true
                Layout.preferredHeight: 108
                enabled: !root.clearHeadersValue
                placeholderText: "{\n  \"X-Tenant\": \"tenant-id\"\n}"
                Component.onCompleted: Qt.callLater(function() {
                    text = root.headersValue
                })
                onTextEdited: {
                    if (text !== root.headersValue) root.headersEdited(text)
                }
                Connections {
                    target: root
                    function onHeadersValueChanged() {
                        if (headersEdit.text !== root.headersValue) {
                            headersEdit.text = root.headersValue
                        }
                    }
                }
            }
        }
    }
}
